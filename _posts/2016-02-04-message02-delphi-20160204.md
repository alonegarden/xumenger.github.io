---
layout: post
title: 理解Delphi消息
categories: delphi之消息机制 windows之消息机制
tags: delphi windows 消息机制 消息系统
---

>直接通过看《DELPHI 5 开发人员指南》学习以下的知识内容

## Delphi的消息系统

VCL替你处理了许多Windows消息系统的细节。例如，消息循环已经封装到VCL的Forms单元，因此，用不着考虑怎样从消息队列中检索消息然后再把它指派给某个窗口。Delphi还把Windows的TMsg记录中的信息映射为TMessage记录：

```
type
  TMessage = record
    Msg: Cardinal;
    case Integer of
      0:(
        WParam: Longint;
        LParam: Longint;
        Result: Longint);
      1:(
        WParamLo: Word;
        WParamHi: Word;
        LParamLo: Word;
        LParamHi: Word;
        ResultLo: Word;
        ResultHi: Word);
  end;
```

需要注意的是，TMessage记录的信息要比TMsg中的信息少一些，这是因为Delphi把这些消息封装起来，TMessage中只包含了处理消息所必须的信息。

特别要注意的是，TMessage记录中包含了一个Result域。正如前面提到的那样，某些信息需要窗口过程返回一个值。在Delphi中，要返回一个值，只要对TMessage记录的Result域赋值。

## 特殊的消息记录

除了通用的TMessage记录外，Delphi为每个Windows消息定义了一个特殊的消息记录，这样就不必从wParam域和lParam域中分解出有关信息。这些特殊的记录可以在Message单元中找到。作为一个例子，下面是一个鼠标的消息记录：

```
type
  TWMMouse = record
    Msg: Cardinal;
    Keys: Longint;
    case Integer of 
      0:(
        XPos: Smallint;
        YPos: Smallint);
      1:(
        Pos: TSmallPoint;
        Result: Longint);
  end;
```

其他鼠标消息（如WM_LBUTTONDOWM和WM_RBUTTONUP），他们的消息记录像下面这样声明：

```
TWMRButtonUp = TWMMouse;
TWMLButtonDown = TWMMouse;
```

注意，几乎每个Windows消息都定义一个消息记录。消息记录的命名是在消息标识符的前面加上大写字母T并去掉下划线。例如，与消息WM_SETFONT对应的消息记录名称叫TWMSetFont。

顺便说一下，TMessage记录对所有的消息都是适用的，而特殊的消息记录只适用于某些消息。

## 消息处理

所谓消息处理，就是应用程序以某种方式响应Windows消息。在一个标准的Windows应用程序中，消息是有窗口过程处理的。不过，Delphi封装了窗口过程，这使得消息处理简单多了。从前，一个窗口程序需要处理各种消息，而在Delphi中，每个消息都有各自的过程。用于消息处理的过程必须满足以下3个条件：

* 这个过程必须是一个对象中的方法
* 这个过程必须有一个**var**参数，变量的类型是TMessage或其他特殊的消息记录
* 声明这个过程时，必须使用**message**指示符，后面跟要处理的消息的常量值。

下面是声明一个处理WM_PAINT消息的过程的代码

```
procedure WMPaint(var Msg: TWMPaint); message WM_PAINT;
```

>注意，给用于消息处理的过程命名时采用这样的约定：**过程名与消息的标识符一致，但不要全部大写，也不要有下划线**。

作为一个例子，下面写一个处理WM_PAINT消息的过程。

首先，创建一个新的空白的项目。然后访问这个项目的代码编辑窗口并在 TForm对象的private部分加入下面这一行。

```
procedure WMPaint(var Msg: TWMPaint); message WM_PAINT;
```

然后，在单元的implementation部分实现这个过程。记住，要在这个过程的名称前加TForm1，但不许有message指示符

```
procedure TForm1.WMPaint(var Msg: TWMPaint);
begin
  Beep;
  inherited;
end;
```

请注意上述代码中的inherited语句。**当想传递消息给祖先对象的处理过程时，请调用inherited语句**。这个语句的作用是把消息传递给TForm中处理WM_PAINT消息的过程

>注意 调用inherited时不需要给出祖先类中方法的名称。因为方法的名称并不重要，Delphi是根据消息的常量来区分过程的。

下面列出了一个完整的单元，它演示了怎样处理WM_PAINT消息。要创建这个项目是很简单的，只要先创建一个空白的项目，然后在 TForm1对象的声明中声明一个过程。

```
unit GMMain;

interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms, Dialogs;

type
  TForm1 = class(TForm)
  private
    procedure WMPaint(var Msg: TWMPaint); message WM_PAINT;
  public
    { Public declarations }
  end;

var
  Form1: TForm1;

implementation

{$R *.dfm}

procedure TForm1.WMPaint(var Msg: TWMPaint);
begin
  MessageBeep(0);
  inherited;
end;

end.
```

当WM_PAINT消息发生时，它就被传递给WMPaint过程，WMPaint过程则调用MessageBeep()来发出声音，然后把消息传递给祖先类的过程。

### 消息处理：不是无约定的

与响应Delphi事件不同的是，处理Windows消息不是无约定的。当在处理一个消息时，Windows总是指望做一些事情。VCL已经内置了基本的消息处理功能，必须要做的就是调用inherited。可以这样认为：写一个消息处理过程是为了做一些你要做的事情，调用inherited是为了做一些Windows要做的事情。

>注意在处理消息时，还有些事情是有限制的。例如，在处理WM_KILLFOCUS消息时，就不允许将焦点设给另一个控件。

为了证明inherited的作用，可以在上面的代码中把WMPaint()方法中的inherited去掉，即代码变成：

```
procedure TForm1.WMPaint(var Msg: TWMPaint);
begin
  MessageBeep(0);
end;
```

因为这样Windows就没有机会对WM_PAINT消息进行基本的处理，所以，窗体就不会被画出来。

有时候，可能不希望调用 inherited。例如，在处理WM_SYSCOMMAND消息时，为了防止窗口被最大化或最小化，就不能调用inherited。

>总之就是在深刻理解了其中的原理之后，更需要自己的灵活应用。

### 对Result域赋值

当处理某些Windows消息时，Windows希望返回一个值。典型的例子是 WM_CTLCOLOR消息。当处理这个消息时， Windows望返回一个画刷的句柄， Windows用这个画刷来画对话框或控件(Delphi为每个组件提供了 Color属性，这个例子只是为了说明问题 )。

**要返回一个值，只要在消息处理过程中对TMessage(或其他消息记录)中的Result域赋值**。例如，当处理WM_CTLCOLOR消息时，可以这样返回一个画刷的句柄：

```
procedure TForm1.WMCtlColor(var Msg: TWMCtlColor);
var
  BrushHand: hBrush;
begin
  inherited;
  {创建一个画刷的句柄，并放进BrushHand变量}
  Msg.Result:= BrushHand;
end;
```

### TApplication的OnMessage事件

要处理Windows的消息，也可以利用TApplication的OnMessage事件。建立了响应OnMessage事件的处理过程后，只要从消息队列中检索到一个消息，就会触发OnMessage事件。这样，在Windows本对消息处理之前就会调用响应 OnMessage事件的处理过程。响应Application.OnMessage事件的处理过程必须是TMessageEvent类型，可以这样声明：

```
procedure SomeObject.AppMessageHandler(var Msg: TMsg;
  var Handled: Boolean);
```

所有消息参数都是通过Msg传递过来的（注意前面讲过Msg参数是Windows TMsg记录类型）。要对Handled参数赋一个布尔值，以表明是否已经处理了这个消息。

要建立一个响应OnMessage事件的处理过程，第一步是声明和定义一个TMessageEvent类型的方法。例如，下面就定义了一个方法，用于统计应用程序总共收到多少消息：

```
var
  NumMessages: Integer;

procedure Form1.AppMessageHandler(var Msg: TMsg; var Handled: Boolean);
begin
  Inc(NumMessages);
  Handled:= False;
end;
```

第二步也是最后一步，就是把上面这个方法赋给Application.OnMessage事件。**这要在DPR文件中，在创建了项目的窗体之后单调用Application.Run之前加入下面语句**：

```
Application.OnMessage := Form1.AppMessageHandler;
```

**OnMessage的一个限制是，它响应的是从消息队列中检索到的消息，而不是直接发给某个窗体程序的消息**。

>提示：OnMessage事件将捕获到发送给应用程序的所有消息。这是一个非常繁忙的事情（每秒钟可能有数千个消息）。因此，在处理OnMessage事件的处理过程中，不可能什么消息都处理，那样会耗费很多时间，使应用程序变慢。**显然，在处理OnMessage事件的处理过程中设断点是非常不明智的**
