---
layout: post
title: Delphi：进程A给进程B频繁发生消息导致进程B卡死
categories: delphi之多线程 delphi之消息机制 delphi之多进程
tags: delphi 线程 窗体 消息机制 多进程 线程
---

## 说明

* [【上一篇】](http://www.xumenger.com/delphi-message-1-20160521/)有讲到一个进程的某个线程持续给自己持续发消息导致卡死
* 本篇扩展到进程A持续给进程B发消息导致进程B卡死
* 本文对应的程序源码可以点击[【这里】](../download/20160521/TestMessage.zip)下载

## 各种情况对应的现象描述

* 什么是卡死：就是进程无法响应用户拖动窗体、关闭窗体等的操作
  * 用户拖动窗体、关闭窗体其实也是通过消息来实现的
  * 卡死就说明进程需要处理的消息太多，来不及处理用户拖动等的消息
* 这里所说的频繁，指的是很高的频率
* 如果在进程A中通过一个Timer每1ms给进程B发消息
  * 使用同步发送消息的正常不会导致B卡死
  * 使用异步发送消息的方式正常也不会导致B卡死
* 所以使用Timer且这只Interval为1ms的频率远远不够，还是要使用线程
* 分别实现同步发送和异步发送的线程，并且不设置Sleep，所以是极其频繁的发消息
  * 进程A中有一个线程同步给进程B发消息，进程B不会卡死
  * 进程A中有一个线程异步给进程B发消息，则进程B会卡死，无法进行拖动、关闭等操作
  * 进程A中有两个线程同步给进程B发消息，则进程B会卡死！
  * 本文对应的例程中没有实现多线程同步发送消息
  * 可以这样实现：再拷贝一份发送消息的进程，两个进程同时点击【使用线程同步发消息】就可以实现多线程发同步消息
* 关于同步、异步发消息可以参考[《Delphi消息机制：发送自己的消息》](http://www.xumenger.com/message03-delphi-20160204/)
* 建议运行一下这个测试程序，观看收消息进程收消息、处理消息的速度，大致对于消息处理的性能有一个直观的了解

## 进程A(发消息)的代码

```
unit SendFrm;

interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  Dialogs, ComCtrls, StdCtrls, ExtCtrls;
const
  WM_DATA=WM_User + 777;

type
  TForm1 = class(TForm)
    pnl1: TPanel;
    btnPost: TButton;
    btnSend: TButton;
    edtTime: TEdit;
    udTime: TUpDown;
    tmrPost: TTimer;
    tmrSend: TTimer;
    pnl2: TPanel;
    lbl1: TLabel;
    lbl2: TLabel;
    btnPostThread: TButton;
    btnSendThread: TButton;
    lbl3: TLabel;
    procedure btnSendClick(Sender: TObject);
    procedure tmrPostTimer(Sender: TObject);
    procedure btnPostClick(Sender: TObject);
    procedure tmrSendTimer(Sender: TObject);
    procedure FormCreate(Sender: TObject);
    procedure btnPostThreadClick(Sender: TObject);
    procedure btnSendThreadClick(Sender: TObject);
  private
    { Private declarations }
  public
    { Public declarations }
  end;

  TPostThread = class(TThread)
  public
    procedure Execute; override;
  end;

  TSendThread = class(TThread)
  public
    procedure Execute; override;
  end;

var
  Form1: TForm1;
  postThread: TPostThread;
  sendThread: TSendThread;

implementation

{$R *.dfm}

procedure TForm1.btnPostClick(Sender: TObject);
begin
  if '异步发消息' = btnPost.Caption then
  begin
    tmrPost.Interval := StrToInt(edtTime.Text);
    tmrPost.Enabled := True;
    btnPost.Caption := '暂停';
    btnSend.Enabled := False;
    btnPostThread.Enabled := False;
    btnSendThread.Enabled := False;
    tmrSend.Enabled := False;
  end
  else
  begin
    tmrPost.Enabled := False;
    btnSend.Enabled := True;
    
    btnPostThread.Enabled := True;
    btnSendThread.Enabled := True;

    btnPost.Caption := '异步发消息';
  end;
end;

procedure TForm1.btnSendClick(Sender: TObject);
begin
  if '同步发消息' = btnSend.Caption then
  begin
    tmrSend.Interval := StrToInt(edtTime.Text);
    tmrSend.Enabled := True;
    btnSend.Caption := '暂停';
    btnPost.Enabled := False;      
    btnPostThread.Enabled := False;
    btnSendThread.Enabled := False;
    tmrPost.Enabled := False;
  end
  else
  begin
    tmrSend.Enabled := False;
    btnPost.Enabled := True;
    
    btnPostThread.Enabled := True;
    btnSendThread.Enabled := True;
    
    btnSend.Caption := '同步发消息';
  end;
end;

procedure TForm1.btnPostThreadClick(Sender: TObject);
begin
  if '异步发消息' = btnPostThread.Caption then
  begin
    postThread.Resume;
    btnPostThread.Caption := '暂停';

    tmrSend.Enabled := False;
    tmrPost.Enabled := False;

    btnPost.Enabled := False;
    btnSendThread.Enabled := False;
    btnSend.Enabled := False;
  end
  else
  begin
    postThread.Suspend;

    btnPost.Enabled := True;
    btnSend.Enabled := True;
    btnPostThread.Enabled := True;
    btnSendThread.Enabled := True;

    btnPostThread.Caption := '异步发消息';
  end;
end;

procedure TForm1.btnSendThreadClick(Sender: TObject);
begin
  if '同步发消息' = btnSendThread.Caption then
  begin
    sendThread.Resume;
    btnSendThread.Caption := '暂停';

    tmrSend.Enabled := False;
    tmrPost.Enabled := False;

    btnPost.Enabled := False;
    btnPostThread.Enabled := False;
    btnSend.Enabled := False;
  end
  else
  begin
    sendThread.Suspend;

    btnPost.Enabled := True;
    btnSend.Enabled := True;
    btnPostThread.Enabled := True;
    btnSendThread.Enabled := True;

    btnSendThread.Caption := '同步发消息';
  end;
end;

procedure TForm1.tmrPostTimer(Sender: TObject);
begin
  //异步发消息
  PostMessage(FindWindow(nil, '收消息'), WM_DATA, 1, 1);
end;

procedure TForm1.tmrSendTimer(Sender: TObject);
begin
  //同步发消息
  SendMessage(FindWindow(nil, '收消息'), WM_DATA, 1, 1);
end;

procedure TForm1.FormCreate(Sender: TObject);
begin
  postThread := TPostThread.Create(True);
  sendThread := TSendThread.Create(True);

  tmrPost.Enabled := False;
  tmrSend.Enabled := False;
  tmrPost.Interval := 1;
  tmrSend.Interval := 1;

  udTime.Min := 1;    //因为假如设置Timer的Interval为0，定时器其实是不会启动的
  
end;

procedure TPostThread.Execute;
begin
  while not Terminated do
  begin
    PostMessage(FindWindow(nil, '收消息'), WM_DATA, 1, 1);
  end;
end;

procedure TSendThread.Execute;
begin
  while not Terminated do
  begin
    SendMessage(FindWindow(nil, '收消息'), WM_DATA, 1, 1);
  end;
end;

end.
```

## 进程B(收消息处理消息)的源码

```
unit RecvFrm;

interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  Dialogs, StdCtrls;

const
  WM_DATA = WM_USER + 777;

type
  TForm1 = class(TForm)
    mmo1: TMemo;
    procedure FormCreate(Sender: TObject);
  private
    { Private declarations }
  public
    procedure getInfo(var msg: TMessage); message WM_DATA;
  end;

var
  Form1: TForm1; 
  index: Integer;

implementation

{$R *.dfm}

procedure TForm1.getInfo(var msg: TMessage);
begin
  if 1 = msg.LParam then
  begin
    Inc(index);
    mmo1.Lines.Add('收到信息数量:' + IntToStr(index));
  end;
end;

procedure TForm1.FormCreate(Sender: TObject);
begin
  index := 0;
  Form1.Caption := '收消息';
end;

end.
```
